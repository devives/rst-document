plugins {
    id 'java-library'
    id "com.github.hierynomus.license" version "0.16.1"
    id "maven-publish"
    id 'signing'
}

group = 'com.devives'
archivesBaseName = "devive-rst-document"
version = '0.2.0'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileJava.options.encoding = "UTF-8"
compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

javadoc.options.encoding = "UTF-8"
javadoc.failOnError = false

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.platform:junit-platform-suite'
}

java {
    withJavadocJar()
    withSourcesJar()
}

var manifest = manifest {
    attributes = [
            "Manifest-Version"      : "1.0",
            "Name"                  : "com/devives/rst",
            "Specification-Title"   : "ReStructuredText Document & Builder",
            "Specification-Version" : "$project.version",
            "Specification-Vendor"  : "Vladimir Ivanov",
            "Implementation-Title"  : "com.devives.rst",
            "Implementation-Version": "$project.version",
            "Implementation-Vendor" : "Vladimir Ivanov",
            "Build-Date"            : new Date().format("yyyy-MM-dd HH:mm:ss (Z)"),
            "X-Compile-Source-JDK"  : "$sourceCompatibility",
            "X-Compile-Target-JDK"  : "$targetCompatibility"
    ]
}

jar {
    setManifest(manifest)

    into("META-INF/maven/$project.group/$archivesBaseName") {
        from { generatePomFileForRstDocPublication }
        rename ".*", "pom.xml"
    }

    into("META-INF") {
        from { "LICENSE-GPL-2.0" }
    }
}

sourcesJar {
    setManifest(manifest)

    into("META-INF/maven/$project.group/$project.name") {
        from { generatePomFileForRstDocPublication }
        rename ".*", "pom.xml"
    }

    into("META-INF") {
        from { "LICENSE-GPL-2.0" }
    }
}

license {
    header project.file('codequality/HEADER')
    strictCheck true
}

publishing {
    publications {
        RstDoc(MavenPublication) {
            artifactId = archivesBaseName

            from components.java
            pom {
                packaging = "jar"
                artifactId = archivesBaseName
                url = "https://github.com/devives/rst-document"
                description = "ReStructuredText Document & Builder"

                properties = [
                        "project.build.sourceEncoding": "UTF-8"
                ]

                organization {
                    name = "DEVIVES"
                    url = "https://github.com/devives"
                }

                licenses {
                    license {
                        name = "GNU General Public License Version 2.0"
                        url = "https://www.gnu.org/licenses/old-licenses/gpl-2.0.txt"
                    }
                }

                scm {
                    connection = "scm:https://github.com/devives/rst-document.git"
                    developerConnection = "scm:git@github.com:devives/rst-document.git"
                    url = "https://github.com/devives/rst-document"
                }

                developers {
                    developer {
                        id = "ivvlev"
                        name = "Vladimir Ivanov"
                        email = "ivvlev@devives.com"
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = 'buildRepo'
            url = layout.buildDirectory.dir("repo")
        }
    }

}

signing {
    sign publishing.publications.RstDoc
}


test {
    useJUnitPlatform()
}
